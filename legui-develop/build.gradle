plugins {
    id "org.sonarqube" version "2.7"
}

group 'org.liquidengine'

import groovy.json.JsonSlurper

// SETUP VERSION
def versionJson = new JsonSlurper().parseText(new File("$project.projectDir/version.json").text)
def versionExt = System.getProperty("VEXT") ?: System.getenv("VEXT") ?: ""
project.version = versionJson.version + (versionExt == "" ? "" : "-" + versionExt)

println("VERSION : " + project.version)


configurations.all {
    resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'maven-publish'

idea { module { downloadJavadoc = true; downloadSources = true } }
sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

//@formatter:off
def log4j_version        = '2.3'
def commons_version      = '3.4'
def commons_collections  = '4.1'
def guava_version        = '19.0'
def gson_version         = '2.7'

def joml_version         = "1.9.18-SNAPSHOT"
def lwjgl_version        = '3.2.3-SNAPSHOT'
def le_cbchain           = '1.0.0'
def le_util              = '1.0.0'
//@formatter:on

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://raw.githubusercontent.com/SpinyOwl/repo/snapshots" }
    maven { url "https://raw.githubusercontent.com/SpinyOwl/repo/releases" }
}

dependencies {
    // https://mvnrepository.com/artifact/org.reflections/reflections
    compile group: 'org.reflections', name: 'reflections', version: '0.9.11'

    //@formatter:off
    compile group: "org.joml",                   name: "joml",                 version: joml_version,          changing: true
    compile group: 'org.apache.logging.log4j',   name: 'log4j-core',           version: log4j_version,         changing: false
    compile group: 'org.apache.commons',         name: 'commons-lang3',        version: commons_version,       changing: false
    compile group: 'org.apache.commons',         name: 'commons-collections4', version: commons_collections,   changing: false
    compile group: 'com.google.guava',           name: 'guava',                version: guava_version,         changing: false
    compile group: 'com.google.code.gson',       name: 'gson',                 version: gson_version,          changing: false

    compile group: 'org.liquidengine',           name: 'cbchain',              version: le_cbchain,            changing: true
    compile group: 'org.liquidengine',           name: 'leutil',               version: le_util,               changing: true

    compile group: 'org.lwjgl',                  name: 'lwjgl-stb',            version: lwjgl_version,         changing: true
    runtime group: 'org.lwjgl',                  name: 'lwjgl-stb',            version: lwjgl_version,         changing: true,  classifier: 'natives-windows'
    runtime group: 'org.lwjgl',                  name: 'lwjgl-stb',            version: lwjgl_version,         changing: true,  classifier: 'natives-linux'
    runtime group: 'org.lwjgl',                  name: 'lwjgl-stb',            version: lwjgl_version,         changing: true,  classifier: 'natives-macos'

    compile group: 'org.lwjgl',                  name: 'lwjgl-glfw',           version: lwjgl_version,         changing: true
    runtime group: 'org.lwjgl',                  name: 'lwjgl-glfw',           version: lwjgl_version,         changing: true,  classifier: 'natives-windows'
    runtime group: 'org.lwjgl',                  name: 'lwjgl-glfw',           version: lwjgl_version,         changing: true,  classifier: 'natives-linux'
    runtime group: 'org.lwjgl',                  name: 'lwjgl-glfw',           version: lwjgl_version,         changing: true,  classifier: 'natives-macos'

    compile group: 'org.lwjgl',                  name: 'lwjgl-nanovg',         version: lwjgl_version,         changing: true
    runtime group: 'org.lwjgl',                  name: 'lwjgl-nanovg',         version: lwjgl_version,         changing: true,  classifier: 'natives-windows'
    runtime group: 'org.lwjgl',                  name: 'lwjgl-nanovg',         version: lwjgl_version,         changing: true,  classifier: 'natives-linux'
    runtime group: 'org.lwjgl',                  name: 'lwjgl-nanovg',         version: lwjgl_version,         changing: true,  classifier: 'natives-macos'

    compile group: 'org.lwjgl',                  name: 'lwjgl-opengl',         version: lwjgl_version,         changing: true
    compile group: 'org.lwjgl',                  name: 'lwjgl-opengl',         version: lwjgl_version,         changing: true,  classifier: 'natives-windows'
    compile group: 'org.lwjgl',                  name: 'lwjgl-opengl',         version: lwjgl_version,         changing: true,  classifier: 'natives-linux'
    compile group: 'org.lwjgl',                  name: 'lwjgl-opengl',         version: lwjgl_version,         changing: true,  classifier: 'natives-macos'

    compile group: 'org.lwjgl',                  name: 'lwjgl',                version: lwjgl_version,         changing: true
    runtime group: 'org.lwjgl',                  name: 'lwjgl',                version: lwjgl_version,         changing: true,  classifier: 'natives-windows'
    runtime group: 'org.lwjgl',                  name: 'lwjgl',                version: lwjgl_version,         changing: true,  classifier: 'natives-linux'
    runtime group: 'org.lwjgl',                  name: 'lwjgl',                version: lwjgl_version,         changing: true,  classifier: 'natives-macos'

    compile group: 'org.lwjgl',                  name: 'lwjgl-yoga',           version: lwjgl_version,         changing: true
    runtime group: 'org.lwjgl',                  name: 'lwjgl-yoga',           version: lwjgl_version,         changing: true,  classifier: 'natives-windows'
    runtime group: 'org.lwjgl',                  name: 'lwjgl-yoga',           version: lwjgl_version,         changing: true,  classifier: 'natives-linux'
    runtime group: 'org.lwjgl',                  name: 'lwjgl-yoga',           version: lwjgl_version,         changing: true,  classifier: 'natives-macos'
    //@formatter:on
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

// PUBLISH SECTION
configurations {
    deployerJar
}
repositories {
    maven { url 'https://raw.githubusercontent.com/synergian/wagon-git/releases' }
}
dependencies {
    deployerJar 'ar.com.synergian:wagon-git:0.3.0'
}

def publishUrl = System.getProperty("publishUrl") ?: System.getenv("publishUrl") ?: null
def publishUsername = System.getProperty("publishUsername") ?: System.getenv("publishUsername") ?: null
def publishPassword = System.getProperty("publishPassword") ?: System.getenv("publishPassword") ?: null

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJar
        repository(url: publishUrl) {
            authentication(userName: publishUsername, password: publishPassword)
        }
    }
}

jar {
    exclude("org/liquidengine/legui/demo/**")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from(sourceSets.main.allSource) {
        exclude("org/liquidengine/legui/demo/**")
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from(javadoc.destinationDir) {
        exclude("org/liquidengine/legui/demo/**")
    }
}

javadocJar.dependsOn javadoc
uploadArchives.dependsOn jar
uploadArchives.dependsOn sourcesJar
uploadArchives.dependsOn javadocJar

artifacts {
    archives sourcesJar
    archives javadocJar
}


// SONARQUBE CONFIGURATION


def sonarLogin = System.getProperty("sonarLogin") ?: System.getenv("sonarLogin") ?: null
sonarqube {
    properties {
        property "sonar.projectKey", "SpinyOwl_legui"
        property "sonar.organization", "spinyowl"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", sonarLogin
    }
}